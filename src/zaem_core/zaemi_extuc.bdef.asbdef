managed implementation in class zbp_aemi_extuc unique;
strict ( 2 ); //Uncomment this line in order to enable strict mode 2. The strict mode has two variants (strict(1), strict(2)) and is prerequisite to be future proof regarding syntax and to be able to release your BO.
with draft;

define behavior for ZAEMI_EXTUC alias ExtUseCase
persistent table zaema_extuc
draft table zaemdr_extuc
lock master total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( global )
{
  create;
  update;
  delete;

  field ( numbering : managed, readonly ) Id;
  field ( readonly ) LastChangedAt, LocalLastChangedAt;

  determination checkConsistency on save { create; update; }

  draft action ( features : instance ) Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;

  mapping for zaema_extuc
    {
      Id                 = id;
      Title              = title;
      AreaId             = area_id;
      BusinessContext    = business_context;
      Diagram            = diagram;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }

  association _Scene { create ( features : global ); with draft; }
  association _ExtTask { create ( features : global ); with draft; }
}

define behavior for ZAEMI_SCENE alias Scene
authorization dependent
persistent table zaema_scene
draft table zaemdr_scene
etag master LocalLastChangedAt
lock dependent by _UseCase

{
  update;
  delete;

  field ( numbering : managed, readonly ) Id;
  field ( readonly ) UseCaseId, LastChangedAt, LocalLastChangedAt;

  mapping for zaema_scene
    {
      Id                 = id;
      UseCaseId          = use_case_id;
      Title              = title;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }

  association _UseCase { with draft; }

}

define behavior for ZAEMI_EXTTASK alias ExtTask
authorization dependent
persistent table zaema_exttask
draft table zaemdr_exttask
etag master LocalLastChangedAt
lock dependent by _UseCase

{
  update;
  delete;

  field ( numbering : managed, readonly ) Id;
  field ( readonly ) UseCaseId, LastChangedAt, LocalLastChangedAt;

  mapping for zaema_exttask
    {
      Id                        = id;
      UseCaseId                 = use_case_id;
      Name                      = name;
      TierId                    = tier_id;
      ExtensionStyleId          = extension_style_id;
      ExtensionDomainId         = extension_domain_id;
      OnStackDomainId           = on_stack_domain_id;
      ExtensionTierId           = extension_tier_id;
      TechnicalExtensionBlockId = technical_extension_block_id;
      Guidance                  = guidance;
      Reasoning                 = reasoning;
      SceneId                   = scene_id;
      RequirementId             = requirement_id;
      LastChangedAt             = last_changed_at;
      LocalLastChangedAt        = local_last_changed_at;
    }

  association _UseCase { with draft; }

}